plugins {
    id 'java'
    id 'antlr'
    id "application"
}

group 'pascal'

repositories {
    mavenCentral()
}

dependencies {
    antlr "org.antlr:antlr4:4.8" // use ANTLR version 4
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-visitor", "-no-listener", "-package", "pascal"]
    outputDirectory = file("build/generated-src/java")
}

sourceSets {
    main {
        java.srcDirs = ['src/main/java', 'build/generated-src/java']
    }
}

compileJava.dependsOn(generateGrammarSource)

application.mainClassName = 'pascal.Main'

jar {
    manifest {
        attributes 'Main-Class': mainClassName,
                'Class-Path': configurations.runtimeClasspath.files.collect {"$it.name"}.join(' ')
    }
}

task runInterpreter(type: Exec) {
    commandLine 'build/install/Pascal/bin/Pascal', 'src/test/fib_test.pas'
    ignoreExitValue true
}

task runTests(type: Exec) {
    commandLine 'bash', 'runTests.bash'
    ignoreExitValue true
}